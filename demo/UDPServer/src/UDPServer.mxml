<?xml version="1.0" encoding="utf-8"?>
<!--

AIR Desktop application, using the builtin DatagramSocket class to
listen for UDP traffic.

The datagramsocket listens on port 1234.
It displays the messages in the textarea, and plays a beat sound
when it receives an amf object with command property set to "PLAY"

-->
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   applicationComplete="applicationCompleteHandler(event)">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import mx.events.FlexEvent;
			
			private var datagramSocket:DatagramSocket;
			
			protected function applicationCompleteHandler(event:FlexEvent):void
			{
				//listen to udp input
				datagramSocket = new DatagramSocket();
				datagramSocket.addEventListener(DatagramSocketDataEvent.DATA, dataHandler);
				datagramSocket.bind(1234);
				datagramSocket.receive();
			}
			
			protected function dataHandler(event:DatagramSocketDataEvent):void
			{
				//messagesArea.appendText(event.data.readUTFBytes(event.data.bytesAvailable) + "\n");
				try
				{
					var o:Object = event.data.readObject();
					switch(o.command)
					{
						case "PLAY":
							messagesArea.appendText("play note\n");
							var s:Sound = new Beat();
							s.play();
							break;
						default:
							messagesArea.appendText("unknown command: " + o.command + "\n");
							break;
					}
				}
				catch(e:Error)
				{
					event.data.position = 0;
					messagesArea.appendText(event.data.readUTFBytes(event.data.bytesAvailable));
				}
			}
			
		]]>
	</fx:Script>
	
	<s:layout>		
		<s:VerticalLayout paddingBottom="10" paddingLeft="10" paddingRight="10" paddingTop="10" />
	</s:layout>
	
	<s:TextArea width="100%" height="100%" id="messagesArea" />
	
</s:WindowedApplication>
